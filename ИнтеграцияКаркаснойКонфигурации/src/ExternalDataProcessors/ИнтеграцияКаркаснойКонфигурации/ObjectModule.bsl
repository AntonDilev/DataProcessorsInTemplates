
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ГлавныйКонтекст;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Инициализирует обработку, устанавливая главный контекст для дальнейшей работы.
//
// Параметры:
//   ПараметрыИнициализации - Структура:
//       * ГлавныйКонтекст - ВнешниеОбработки.СозданиеЗаказов - контекст основной обработки.
//
//@skip-check doc-comment-type
Процедура Инициализировать(ПараметрыИнициализации) Экспорт
	
	ГлавныйКонтекст = ПараметрыИнициализации.ГлавныйКонтекст;
	
КонецПроцедуры

Процедура ЗаполнитьТипыДанныхДляРеквизитов() Экспорт
	
	ТипыДанныхДляРеквизитов = ГлавныйКонтекст.ПараметрыКонтекста.ТипыДанныхДляРеквизитов;
	ТипыДанныхДляРеквизитов.Контрагенты = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	ТипыДанныхДляРеквизитов.Номенклатура = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ТипыДанныхДляРеквизитов.ЗаказКлиента = Новый ОписаниеТипов("ДокументСсылка.РасходнаяНакладная");
	
КонецПроцедуры

Процедура СоздатьЗаказ() Экспорт
	
	РасходнаяНакладная = Документы.РасходнаяНакладная.СоздатьДокумент();
	РасходнаяНакладная.Заполнить(Неопределено);
	РасходнаяНакладная.Дата = ГлавныйКонтекст.ДатаДокумента;
	
	СписокНоменклатуры = РасходнаяНакладная.СписокНоменклатуры;
	Для Каждого СтрокаЗаказа Из ГлавныйКонтекст.Товары Цикл
		
		СтрокаДокумента = СписокНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаЗаказа);
		СтрокаДокумента.Сумма = СтрокаДокумента.Количество * СтрокаДокумента.Цена;
		
	КонецЦикла;
	
	РасходнаяНакладная.СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
	
	Если РасходнаяНакладная.ПроверитьЗаполнение() Тогда
		
		Попытка
			РасходнаяНакладная.Записать();
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Записан документ %1 по контрагенту %2'"),
				РасходнаяНакладная,
				ГлавныйКонтекст.Контрагент);
			СообщитьПользователю(ТекстСообщения);
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		ГлавныйКонтекст.ЗавершитьСозданиеЗаказа(РасходнаяНакладная.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СообщитьПользователю(ТекстСообщения)
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();
	
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
