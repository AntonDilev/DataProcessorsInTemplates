
// Обработка может запускаться на платформе 8.2, в которой не поддерживаются области.

//@skip-check module-accessibility-at-client
//@skip-check module-structure-method-in-regions

//#Область ОписаниеПеременных

//@skip-check module-structure-var-in-region
Перем ГлавныйКонтекст;

//#КонецОбласти

//#Область СлужебныйПрограммныйИнтерфейс

// Инициализирует обработку, устанавливая главный контекст для дальнейшей работы.
//
// Параметры:
//   ПараметрыИнициализации - Структура:
//       * ГлавныйКонтекст - ВнешниеОбработки.СозданиеЗаказов - контекст основной обработки.
//
//@skip-check doc-comment-type
Процедура Инициализировать(ПараметрыИнициализации) Экспорт
	
	ГлавныйКонтекст = ПараметрыИнициализации.ГлавныйКонтекст;
	
КонецПроцедуры

Процедура ЗаполнитьТипыДанныхДляРеквизитов() Экспорт
	
	ТипыДанныхДляРеквизитов = ГлавныйКонтекст.ПараметрыКонтекста.ТипыДанныхДляРеквизитов;
	ТипыДанныхДляРеквизитов.Контрагенты = Новый ОписаниеТипов("СправочникСсылка.Клиенты");
	ТипыДанныхДляРеквизитов.Номенклатура = Новый ОписаниеТипов("СправочникСсылка.Товары");
	ТипыДанныхДляРеквизитов.ЗаказКлиента = Новый ОписаниеТипов("ДокументСсылка.ЗаказКлиента");
	
КонецПроцедуры

Процедура СоздатьЗаказ() Экспорт
	
	ЗаказКлиента = Документы.ЗаказКлиента.СоздатьДокумент();
	ЗаказКлиента.Заполнить(Неопределено);
	
	ЗаказКлиента.Дата = ГлавныйКонтекст.ДатаДокумента;
	ЗаказКлиента.Клиент = ГлавныйКонтекст.Контрагент;
	
	Товары = ЗаказКлиента.Товары;
	Для Каждого СтрокаЗаказа Из ГлавныйКонтекст.Товары Цикл
		
		СтрокаДокумента = Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаЗаказа);
		СтрокаДокумента.Товар = СтрокаЗаказа.Номенклатура;
		СтрокаДокумента.Сумма = СтрокаЗаказа.Количество * СтрокаЗаказа.Цена;
		
	КонецЦикла;
	ЗаказКлиента.СуммаВсего = Товары.Итог("Сумма");
	
	Если ЗаказКлиента.ПроверитьЗаполнение() Тогда
		Попытка
			ЗаказКлиента.Записать();
			
			ТекстСообщения = НСтр("ru = 'Записан документ %1 по контрагенту %2'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ЗаказКлиента.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%2", ГлавныйКонтекст.Контрагент);
			//@skip-check use-non-recommended-method
			Сообщить(ТекстСообщения);
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		ГлавныйКонтекст.ЗавершитьСозданиеЗаказа(ЗаказКлиента.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

//#КонецОбласти

