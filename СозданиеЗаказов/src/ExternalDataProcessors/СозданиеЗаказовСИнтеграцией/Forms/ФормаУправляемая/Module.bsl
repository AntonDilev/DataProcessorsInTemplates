
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ДатаДокумента = ТекущаяДатаСеанса();
	
	ПодготовитьКонтекстОбработки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьСозданияЗаказа(Ложь);
	
	ОпределитьТипыДанныхИзПараметровКонтекста();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы = Ложь)
	// В старых платфмормах параметр ЗавершениеРаботы отсутствует, поэтому занчение по умолчанию Ложь
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтладкаПриИзменении(Элемент)
	
	Если Объект.Отладка Тогда
		
		ДиалогСохранения = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ДиалогСохранения.Заголовок = НСтр("ru = 'Укажите каталог сохранения обработок из макетов (каталог основной обработки)'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбораКаталогаДляОбработок", ЭтотОбъект, ДиалогСохранения);
		ДиалогСохранения.Показать(ОписаниеОповещения);
		
	КонецЕсли;
	
	СохранитьНастройкиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НовыйЗаказ(Команда)
	
	Объект.СсылкаНаСозданныйЗаказ = Неопределено;
	Объект.Контрагент = Неопределено;
	Объект.Товары.Очистить();
	
	УстановитьДоступностьСозданияЗаказа(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗаказ(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		СоздатьЗаказНаСервере();
		
		Если ЗначениеЗаполнено(Объект.СсылкаНаСозданныйЗаказ) Тогда
			УстановитьДоступностьСозданияЗаказа(Ложь);
			ПоказатьПредупреждение(, НСтр("ru = 'Заказ успешно создан'"));
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Заказ не создан'"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьСозданияЗаказа(Доступность = Истина)
	
	Элементы.ФормаЗаписатьЗаказ.Доступность = Доступность;
	Элементы.ДатаДокумента.Доступность = Доступность;
	Элементы.Контрагент.Доступность = Доступность;
	Элементы.Товары.Доступность = Доступность;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьКонтекстОбработки()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ОбработкаОбъект.ВосстановитьНастройкиОбработки();
	
	// Тут тоже происходит вызов метода интеграции для определения типов данных
	ОбработкаОбъект.ИнициализироватьПараметрыКонтекста();
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьТипыДанныхИзПараметровКонтекста()
	
	// Демонстрация взаимодействия с данными на клиенте полученными ранее из модуля интеграции
	// Объект.ПараметрыКонтекста.ТипыДанныхДляРеквизитов - содержит типы данных для соотвтствюующих реквизитов
	ТипыДанныхИзИнтеграции = Объект.ПараметрыКонтекста.ТипыДанныхДляРеквизитов;
	
	Элементы.Контрагент.ОграничениеТипа = ТипыДанныхИзИнтеграции.Контрагенты;
	Объект.Контрагент = ТипыДанныхИзИнтеграции.Контрагенты.ПривестиЗначение(Неопределено);
	
	Элементы.СсылкаНаСозданныйЗаказ.ОграничениеТипа = ТипыДанныхИзИнтеграции.ЗаказКлиента;
	Объект.СсылкаНаСозданныйЗаказ = ТипыДанныхИзИнтеграции.ЗаказКлиента.ПривестиЗначение(Неопределено);
	
	Элементы.ТоварыНоменклатура.ОграничениеТипа = ТипыДанныхИзИнтеграции.Номенклатура;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ОбработкаОбъект.СохранитьНастройкиОбработки();
	
КонецПроцедуры

#Область ВызовМетодовИнтеграции

&НаСервере
Процедура СоздатьЗаказНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	// Демонстрация вызова метода интеграции через модуль обработки
	ОбработкаОбъект.СоздатьЗаказВИнтеграции();
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

#КонецОбласти

#Область ВыгрузкаОбработокИзМакетовДляОтладки

&НаСервере
Функция ОбработкиИзМакетов()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	Возврат ОбработкаОбъект.ФайлыОбработокИзМакетов(УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ЗавершениеВыбораКаталогаДляОбработок(ВыбранныеФайлы, ДиалогСохранения) Экспорт
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		КаталогДляОбработок = ДиалогСохранения.Каталог;
		
		ПолучаемыеФайлыИзМакетов = ОбработкиИзМакетов();
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеПолученияФайловОбработок", ЭтотОбъект);
		//@skip-check bsl-legacy-check-static-feature-access
		НачатьПолучениеФайлов(ОписаниеОповещения, ПолучаемыеФайлыИзМакетов, КаталогДляОбработок, Ложь);
		
	Иначе
		
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран каталог для сохранения обработок'"));
		
		Объект.Отладка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПолученияФайловОбработок(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	Для Каждого СохраненнаяОбработка Из ПолученныеФайлы Цикл
		
		Сообщение = Новый СообщениеПользователю();
		// Примечание: СтрШаблон() доступен только с 8.3.6 или в режиме совместимости выше 8.3.5
		Сообщение.Текст = СтрЗаменить(НСтр("ru = 'Сохранена обработка %1'"), "%1", СохраненнаяОбработка.ПолноеИмя);
		Сообщение.Сообщить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти


