
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область НастройкиОбработки

// Восстанавливает настройки обработки из хранилища общих настроек.
//
//   КлючОбъекта - Строка - Имя метаданного объекта
//   КлючНастройки - Строка - Ключ для поиска настроек в хранилище
//   НастройкиОбработки - Структура - Загруженные настройки обработки
//   Отладка - Булево - Признак режима отладки
Процедура ВосстановитьНастройкиОбработки() Экспорт
	
	КлючОбъекта = Метаданные().Имя;
	КлючНастройки = КлючОбъекта + "_Настройки";
	НастройкиОбработки = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, КлючНастройки);
	
	Если ЗначениеЗаполнено(НастройкиОбработки) Тогда
		Отладка = НастройкиОбработки.Отладка;
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет текущие настройки обработки в хранилище общих настроек.
//
Процедура СохранитьНастройкиОбработки() Экспорт
	
	НастройкиОбработки = Новый Структура;
	НастройкиОбработки.Вставить("Отладка", Отладка);
	
	КлючОбъекта = Метаданные().Имя;
	КлючНастройки = КлючОбъекта + "_Настройки";
	ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, КлючНастройки, НастройкиОбработки);
	
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияГлавногоКонтекста

Процедура ИнициализироватьПараметрыКонтекста() Экспорт
	
	ФайлОбработки = Новый Файл(ИспользуемоеИмяФайла);
	КаталогОбработки = ФайлОбработки.Путь;
	
	ИмяМакетаИнтеграции = ИмяИнтеграцииПоКонфигурации();
	
	ПараметрыКонтекста = Новый Структура;
	ПараметрыКонтекста.Вставить("КаталогОбработки", КаталогОбработки);
	ПараметрыКонтекста.Вставить("ТипыДанныхДляРеквизитов", НовыйТипыДанныхДляРеквизитов());// будут определены из модуля интеграции
	ПараметрыКонтекста.Вставить("ИмяМакетаИнтеграции", ИмяМакетаИнтеграции);
	ПараметрыКонтекста.Вставить("АдресМодуляИнтеграции", Неопределено);// будет определен позже
	ПараметрыКонтекста.Вставить("МодульИнтеграцииПодключен", Ложь);
	
	Если Не ЗначениеЗаполнено(ИмяМакетаИнтеграции) Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось определить интеграцию по имени конфигурации.'");
	КонецЕсли;
	
	Если Отладка Тогда
		ПараметрыКонтекста.АдресМодуляИнтеграции = КаталогОбработки + ИмяМакетаИнтеграции + ".epf";
	Иначе
		ПодключитьОбработкуИзМакета();
	КонецЕсли;
	
	// Тут происходит вызов метода интеграции для определения типов данных
	ОпределитьТипыДанныхИзИнтеграции();
	
КонецПроцедуры

#КонецОбласти

#Область ВызовМетодовМодуляИнтеграции

Процедура ОпределитьТипыДанныхИзИнтеграции() Экспорт
	
	МодульИнтеграции = СоздатьМодульИнтеграции();
	МодульИнтеграции.ЗаполнитьТипыДанныхДляРеквизитов();
	
КонецПроцедуры

Процедура СоздатьЗаказВИнтеграции() Экспорт
	
	МодульИнтеграции = СоздатьМодульИнтеграции();
	МодульИнтеграции.СоздатьЗаказ();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейсГлавногоКонтекста

// Завершает создание заказа передачей ссылки в реквизит СсылкаНаСозданныйЗаказ.
//
// Параметры:
//   СозданныйЗаказ - ДокументСсылка - Ссылка на созданный заказ
Процедура ЗавершитьСозданиеЗаказа(СозданныйЗаказ) Экспорт
	
	СсылкаНаСозданныйЗаказ = СозданныйЗаказ;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеМетодыДляРаботыМодулейИнтеграции

Функция СоздатьМодульИнтеграции()
	
	Если ПараметрыКонтекста.МодульИнтеграцииПодключен Тогда
		МодульИнтеграции = СоздатьВнешнююОбработку(ПараметрыКонтекста.ИмяМакетаИнтеграции);
	Иначе
		МодульИнтеграции = СоздатьВнешнююОбработку(ПараметрыКонтекста.АдресМодуляИнтеграции);
	КонецЕсли;
	
	ПараметрыИнициализацииОбработки = НовыйПараметрыИнициализацииМодуляИнтеграции();
	
	МодульИнтеграции.Инициализировать(ПараметрыИнициализацииОбработки);
	
	Возврат МодульИнтеграции;
	
КонецФункции

Функция НовыйПараметрыИнициализацииМодуляИнтеграции()
	
	ПараметрыИнициализацииОбработки = Новый Структура;
	ПараметрыИнициализацииОбработки.Вставить("ГлавныйКонтекст", ЭтотОбъект);
	
	Возврат ПараметрыИнициализацииОбработки;
	
КонецФункции

Функция НовыйТипыДанныхДляРеквизитов()
	
	ТипыДанныхДляРеквизитов = Новый Структура();
	ТипыДанныхДляРеквизитов.Вставить("Контрагенты", Неопределено);
	ТипыДанныхДляРеквизитов.Вставить("Номенклатура", Неопределено);
	ТипыДанныхДляРеквизитов.Вставить("ЗаказКлиента", Неопределено);
	
	Возврат ТипыДанныхДляРеквизитов;
	
КонецФункции

Функция ИмяИнтеграцииПоКонфигурации()
	
	ИмяКонфигурации = Метаданные.Имя;
	ИмяИнтеграции = ДоступныеИнтеграции().Получить(ИмяКонфигурации);
	
	Возврат ИмяИнтеграции;
	
КонецФункции

Функция ДоступныеИнтеграции()
	
	ДоступныеИнтеграции = Новый Соответствие;
	
	МанифестОбъектов = ТаблицаЗначенийИзМакета("МанифестОбъектов");
	
	ТолькоОбработки = Новый Структура("ТипОбработки", "ОбработкаИнтеграции");
	МанифестыОбработок = МанифестОбъектов.НайтиСтроки(ТолькоОбработки);
	Для Каждого МанифестОбработки Из МанифестыОбработок Цикл
		Если ЗначениеЗаполнено(МанифестОбработки.ИмяКонфигурацииИнтеграции) Тогда
			ДоступныеИнтеграции.Вставить(МанифестОбработки.ИмяКонфигурацииИнтеграции, МанифестОбработки.ИмяМакета);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДоступныеИнтеграции;
	
КонецФункции

#КонецОбласти

#Область РаботаСВнешнимиОбработкамиИОбработкамиВМакетах

// Создает и возвращает внешнюю обработку по переданному имени или пути.
// Если передан путь, то он должен быть доступен с сервера
// 
// Параметры:
//  ИмяИлиПутьОбработки - Строка - Имя подключенной обработки или путь к файлу внешней обработки
// 
// Возвращаемое значение:
//  ВнешняяОбработка - созданная внешняя обработка
Функция СоздатьВнешнююОбработку(ИмяИлиПутьОбработки)
	
	БезопасныйРежим = Ложь;
	ОписаниеЗащитыОтОпасныхДействий = ОписаниеЗащитыОтОпасныхДействийДляОбработок();
	
	Если ОписаниеЗащитыОтОпасныхДействий = Неопределено Тогда
		ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяИлиПутьОбработки, БезопасныйРежим);
	Иначе
		ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяИлиПутьОбработки, БезопасныйРежим, ОписаниеЗащитыОтОпасныхДействий);
	КонецЕсли;
	
	Возврат ВнешняяОбработка;
	
КонецФункции

// Подключает обработку из макета
//
Процедура ПодключитьОбработкуИзМакета()
	
	ИмяМакетаОбработки = ПараметрыКонтекста.ИмяМакетаИнтеграции;
	МакетОбработки = ПолучитьМакет(ИмяМакетаОбработки);
	
	ОписаниеЗащиты = Новый ОписаниеЗащитыОтОпасныхДействий;
	ОписаниеЗащиты.ПредупреждатьОбОпасныхДействиях = Ложь;
	
	БезопасныйРежим = Ложь;
	ОписаниеЗащитыОтОпасныхДействий = ОписаниеЗащитыОтОпасныхДействийДляОбработок();
	
	Если ЭтоТолстыйКлиент() Тогда
		АдресОбработки = ПоместитьВоВременноеХранилище(МакетОбработки, Новый УникальныйИдентификатор);
		//@skip-check missing-temporary-file-deletion
		АдресОбработки = ПолучитьИмяВременногоФайла(".epf");
		МакетОбработки.Записать(АдресОбработки);
	Иначе
		АдресОбработки = ПоместитьВоВременноеХранилище(МакетОбработки);
		Если ОписаниеЗащитыОтОпасныхДействий = Неопределено Тогда
			ВнешниеОбработки.Подключить(АдресОбработки, ИмяМакетаОбработки, БезопасныйРежим);
		Иначе
			ВнешниеОбработки.Подключить(АдресОбработки, ИмяМакетаОбработки, БезопасныйРежим, ОписаниеЗащитыОтОпасныхДействий);
		КонецЕсли;
		УдалитьИзВременногоХранилища(АдресОбработки);
		
		ПараметрыКонтекста.МодульИнтеграцииПодключен = Истина;
	КонецЕсли;
	
	ПараметрыКонтекста.АдресМодуляИнтеграции = АдресОбработки;
	
КонецПроцедуры

// Определяет в каком приложении запущена обработка.
//
// Возвращаемое значение:
//   Булево - Истина, если обработка запущена в толстом клиенте
//            в режиме обычного приложения.
//
Функция ЭтоТолстыйКлиент()
	
	Результат = Ложь;
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Результат = Истина;
	#КонецЕсли
	
	Возврат Результат;
	
КонецФункции

// Возвращает описание защиты от опасных действий с отключенным предупреждением, если платформа это поддерживает.
// 
// Возвращаемое значение:
//   ОписаниеЗащитыОтОпасныхДействий - если описание защиты поддерживается текущей версией платформы.
//   Неопределено                    - если описание защиты не поддерживается текущей версией платформы.
//
Функция ОписаниеЗащитыОтОпасныхДействийДляОбработок()
	
	Результат = Неопределено;
	
	//@skip-check empty-except-statement
	Попытка
		Результат = Новый("ОписаниеЗащитыОтОпасныхДействий");
		Результат.ПредупреждатьОбОпасныхДействиях = Ложь;
	Исключение
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РаботаСМакетами

// Возвращает данные из табличного макета в виде таблицы значений 
// В первой строке должны макета быть имена колонок таблицы значений отвечающие требованиям к именованию колонок.
//
// Параметры:
//  ИмяМакета	 - Строка	 - имя макета с таблицей
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблица полученная из макета
//
Функция ТаблицаЗначенийИзМакета(ИмяМакета)
	
	Макет = ПолучитьМакет(ИмяМакета);
	ОбластьДанных = Макет.Область(1, 1, Макет.ВысотаТаблицы, Макет.ШиринаТаблицы);
	
	ПостроительОтчета = Новый ПостроительОтчета;
	ПостроительОтчета.ИсточникДанных = Новый ОписаниеИсточникаДанных(ОбластьДанных);
	ПостроительОтчета.Выполнить();
	
	ТаблицаЗначений = ПостроительОтчета.Результат.Выгрузить();
	
	Возврат ТаблицаЗначений;
	
КонецФункции

// Возвращает описания передаваемых файлов полученных из макетов содержащих обработки
// 
// Параметры:
//  ИдентификаторВладельца - УникальныйИдентификатор - В случае, если передается УникальныйИдентификатор формы
// или адрес в хранилище, то значение будет автоматически удалено после закрытия этой формы.
// Если передан УникальныйИдентификатор, не являющийся уникальным идентификатором формы,
// то значение будет удалено после завершения сеанса пользователя.
// 
// Возвращаемое значение:
//  Массив из ОписаниеПередаваемогоФайла - файлы обработок из макетов для передачи на клиент
Функция ФайлыОбработокИзМакетов(ИдентификаторВладельца = Неопределено) Экспорт
	
	ПередаваемыеФайлыОбработок = Новый Массив;
	
	МанифестОбъектов = ТаблицаЗначенийИзМакета("МанифестОбъектов");
	
	ТолькоОбработки = Новый Структура("ТипМакета", "Обработка");
	МанифестыОбработок = МанифестОбъектов.НайтиСтроки(ТолькоОбработки);
	Для Каждого МанифестОбработки Из МанифестыОбработок Цикл
		
		ИмяФайлаОбработки = МанифестОбработки.ИмяМакета + ".epf";
		МакетОбработки = ПолучитьМакет(МанифестОбработки.ИмяМакета);
		АдресХранилища = ПоместитьВоВременноеХранилище(МакетОбработки, ИдентификаторВладельца);
		ОписаниеПередаваемогоФайла = Новый ОписаниеПередаваемогоФайла(ИмяФайлаОбработки, АдресХранилища);
		ПередаваемыеФайлыОбработок.Добавить(ОписаниеПередаваемогоФайла);
		
	КонецЦикла;
	
	Возврат ПередаваемыеФайлыОбработок;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

